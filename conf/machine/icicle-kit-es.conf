#@TYPE: Machine
#@NAME: icicle-kit-es
#@SOC: Polarfire SoC
#@DESCRIPTION: Machine configuration for the Microchip Polarfire SoC, Icicle Kit Engineering Sample

require include/mpfs-common-machine-configuration.inc

MACHINE_TYPE = "smp"

RISCV_SBI_FDT ?=  "mpfs-icicle-kit.dtb"
KERNEL_DTC_FLAGS += "-@"
KERNEL_DEVICETREE ?= "microchip/${RISCV_SBI_FDT}"

## Set this to "mpfs_icicle" to use a nand flash as the default external flash memory
## Set this to "mpfs_icicle_nor" to use a nor flash as the default external flash memory
## Note: If no external flash memory is found, U-boot will try to boot from SD/emmc 
## or fallback to tftp

UBOOT_CONFIG = "mpfs_icicle"
UBOOT_CONFIG[mpfs_icicle] = "microchip_mpfs_icicle_defconfig"
UBOOT_CONFIG[mpfs_icicle_nor] = "microchip_mpfs_icicle_qspi_nor_defconfig"

INCLUDES += "${@bb.utils.contains('UBOOT_CONFIG', 'mpfs_icicle', \
' include/qspi-flash-nand.inc', ' include/qspi-flash-nor.inc', d)}"
require ${INCLUDES}


## Set this to "mmc-tftp" to generate a boot.scr file which should be included
##  in the boot partition. It will try to load a kernel image from mmc and fall back to tftp
UBOOT_ENV = "tftp-mmc"

HSS_PAYLOAD = "uboot"

IMAGE_CLASSES:append = " image_type_mtd"

IMAGE_BOOT_FILES ?= " \
    fitImage \
    boot.scr.uimg \
"

WKS_FILE ?= "mpfs-rootfs.wks"

